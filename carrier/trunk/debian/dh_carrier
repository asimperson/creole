#! /usr/bin/perl -w

=head1 NAME

dh_carrier- depend on the appropriate version of carrier

=cut

use strict;
use Debian::Debhelper::Dh_Lib;

=head1 SYNOPSIS

B<dh_carrier> [S<I<debhelper options>>]

=head1 DESCRIPTION

dh_carrier is a debhelper program that is responsible for generating 
the ${misc:Depends} substitutions that depend on the correct versions of carrier,
and adding them to substvars files.

To use this program, make sure it is executed in debian/rules at some point
during the build process (normally during the install), and make sure that
your Depends: field in debian/control contains ${misc:Depends}.

=cut

init();

my ($carrier_epoch, $carrier_version, $next_version, $carrier_major, $carrier_minor,
    $carrier_rest);
$carrier_version = `dpkg -s carrier`;
$carrier_version =~ /^Version:\s*([\S]+)/m;
$carrier_version = $1;

if (! defined $carrier_version) {
     error("carrier is not installed.  (Probably forgot to Build-Depend on carrier)");
}
if ($carrier_version =~ m/(\d+:)?(\d+)\.(\d+)\.(.*)/) {
     $carrier_epoch = $1 || "";
     $carrier_major = $2;
     $carrier_minor = $3;
     $carrier_rest = $4;
} else {
     error("Unable to parse carrier version out of '$carrier_version'");
}

$next_version = $carrier_epoch . ($carrier_major + 1) . ".0";
$carrier_version = $carrier_epoch . $carrier_major . "." .$carrier_minor;

foreach my $package (@{$dh{DOPACKAGES}}) {
	my $tmp=tmpdir($package);
	addsubstvar($package, "misc:Depends", "carrier", ">= $carrier_version");
	addsubstvar($package, "misc:Depends", "carrier", "<< $next_version");
	addsubstvar($package, "misc:Conflicts", "carrier", ">= $next_version");
}

=head1 SEE ALSO

L<debhelper(7)>

=head1 AUTHOR

Written by Tollef Fog Heen <tfheen@debian.org>, based on various other
dh_* commands written by Joey Hess <joeyh@debian.org>.

=cut
